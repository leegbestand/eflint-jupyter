// level 0
Fact person

Duty duty not to kill
  Holder person1
  Claimant person2
  Present when Present person1 && Present person2

// level 1
Fact prosecutor
  Identified by person
Fact suspect
  Identified by person
Fact victim
  Identified by person

Act prosecute
  Actor person3
  Recipient person2
  Related to person1
  Conditioned by 
       suspect[person = person2]
    && victim[person = person1]
    && duty not to kill[person1=person2, person2=person1]
    && killed by[person1 = person1, person2 = person2]
  Terminates
    suspect[person = person2],
    victim[person = person1]
  Present When prosecutor[person = person3]

Fact killed by
  Identified by [person1 * person2]

// level 2
Fact poisoned by
  Identified by [person1 * person2]

// level 3
Fact alive
  Identified by person

Fact substance
Fact food
Fact ingredient 
  Identified by [substance * food]
Fact poison
  Identified by substance

Physical mix
  Actor person
  Related to substance, food
  Conditioned by Present(food)
  Creates 
    ingredient,
    mix-completed

Physical eat
  Actor person
  Related to food
  Conditioned by 
       alive
    && Present(food)
  Creates 
    eat-completed
  Terminates 
    food

Physical qualify poisoning With
  Actor person
  Conditioned by 
       Present(mix-completed[person = person2, substance = poison[substance]])
    && Present(eat-completed[person = person1])
    && !Present(alive[person = person1]) // may have died for another reason, where is connection?
  Creates
    poisoned by

Physical qualify killing With
  Actor person
  Conditioned by poisoned by
  Creates
    killed by,
    suspect[person = person2],
    victim[person = person1]

// should be generated for each plan
Fact mix-completed
  Identified by [person * substance * food] //note that fields should contain any unbound variables
Fact eat-completed
  Identified by [person * food]

##

Fact person
  Identified by ["alice" + "bob" + "chloe" + "david"]
 
Fact substance
  Identified by ["salt" + "sugar" + "polonium" + "rat poison"]

Fact food 
  Identified by ["curry" + "salad"]

###

person. alive. food.
poison[substance="rat poison"]. 
poison[substance="polonium"].
prosecutor[person="david"].

####

!mix[person = "alice", substance = "salt", food = "curry"].
!mix[person = "alice", substance = "rat poison", food = "curry"].
!mix[person = "bob", substance = "sugar", food = "curry"].
!eat[person = "bob", food = "curry"].
-alive[person = "bob"].
!qualify poisoning[person = "chloe"].
!qualify killing[person = "chloe"].
!prosecute[person3 = "david", person2 = "alice", person1 = "bob"].

