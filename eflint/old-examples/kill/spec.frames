// level 0
Fact person

Duty duty not to kill
  Holder person1
  Claimant person2
  Present When True 

// level 1
Placeholder prosecutor For person
Placeholder suspect For person
Placeholder victim For person
Placeholder investigator For person

Act prosecute
  Actor prosecutor
  Recipient suspect
  Related to victim
  Conditioned by
       duty not to kill(person1 = suspect, person2 = victim)
    && killed by(person1 = victim, person2 = suspect)

Fact killed by
  Identified by person1 * person2

// level 2
Fact poisoned by
  Identified by person1 * person2

// level 3
Fact alive
  Identified by person

Fact substance
Fact food
Fact ingredient 
  Identified by substance * food
Fact poison
  Identified by substance

Physical mix
  Actor person
  Related to substance, food
  Conditioned by Present(food)
  Creates 
    ingredient(substance = substance, food = food),
    mix-completed(person = person, substance = substance, food = food)

Physical eat
  Actor person
  Related to food
  Conditioned by 
       alive()
    && Present(food)
  Creates 
    eat-completed(person = person, food = food)
  Terminates 
    food

Physical qualify poisoning With
  Actor investigator
  Recipient suspect
  Related to victim
  Conditioned by // TODO requires evidence of cause of death
       Present(poison)
    && Present(mix-completed(person = suspect, substance = poison.substance, food = food))
    && Present(eat-completed(person = victim, food = food))
    && !Present(alive(person = victim))
    && !Present(poisoned by(person1 = victim, person2 = suspect))
  Creates
    poisoned by(person1 = victim, person2 = suspect)

Physical qualify killing With
  Actor investigator
  Recipient suspect
  Related to victim
  Conditioned by 
    poisoned by(person1 = victim, person2 = suspect)
  Creates
    killed by(person1 = victim, person2 = suspect) // TODO requires evidence of motivation/intent

// should be generated for each plan
Fact mix-completed
  Identified by person * substance * food //note that fields should contain any unbound variables
Fact eat-completed
  Identified by (person * food)


